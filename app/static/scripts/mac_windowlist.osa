tell application "System Events"
    set windowList to {}
    
    -- Fetch all visible processes
    set visibleProcesses to (processes whose visible is true)
    
    -- Iterate through each process
    repeat with p in visibleProcesses
        try
            set pName to name of p
            set pID to id of p
            set winTitles to {}
            
            -- Safely retrieve window names
            set allWindows to windows of p
            repeat with w in allWindows
                try
                    set winName to name of w
                    if winName is not missing value then
                        set end of winTitles to winName
                    end if
                end try
            end repeat
            
            -- Add process only if windows exist
            if (count of winTitles) > 0 then
                set end of windowList to {pName, pID, winTitles}
            end if
        end try
    end repeat
end tell

-- Build JSON
set jsonString to "{ \"data\": ["
set {tid, AppleScript's text item delimiters} to {AppleScript's text item delimiters, ","}

set jsonEntries to {}
repeat with winO in windowList
    set pName to item 1 of winO
    set pID to item 2 of winO
    set winTitles to "\"" & (items of item 3 of winO as text) & "\""
    set end of jsonEntries to "
  {
    \"processName\": \"" & pName & "\",
    \"id\": \"" & pID & "\",
    \"windows\": [" & winTitles & "]
  }"
end repeat

-- Combine JSON entries into one string
set jsonString to jsonString & (jsonEntries as text) & "]}"
set AppleScript's text item delimiters to tid

return jsonString
